plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.13.0'
}

project.version = getProperty("pulsar.version")


subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useTestNG()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.9
                }
            }
        }
    }

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            googleJavaFormat()
            formatAnnotations()
        }
    }

    dependencies {
        testImplementation 'org.testng:testng:7.9.0'
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
        testCompileOnly 'org.projectlombok:lombok:1.18.30'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    }
}